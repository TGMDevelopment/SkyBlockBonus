buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            url = "https://repo.spongepowered.org/maven/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'

version = "0.1"
group = "ga.matthewtgm.skyblockmod"
archivesBaseName = "SkyBlockBonus"

sourceCompatibility = targetCompatibility = 1.8

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"

    replace("@VER@": project.version)

    mappings = "stable_22"
}

dependencies {
    compile('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
    }
    compile('com.jagrosh:DiscordIPC:0.4')
    compile('com.github.TGMDevelopment:JsonTGM:0.2.3')
    compile('com.github.TGMDevelopment:TGMLib:0.7.3')
    provided(files('libs/modcore.jar'))
}

shadowJar {
    dependencies {
        include(dependency('org.spongepowered:mixin:0.7.11-SNAPSHOT'))
        include(dependency('com.jagrosh:DiscordIPC:0.4'))
        include(dependency('com.github.TGMDevelopment:JsonTGM:0.2.3'))
        include(dependency('com.github.TGMDevelopment:TGMLib:0.7.3'))
    }

    exclude('modcore.jar')
    exclude('LICENSE.txt')
    exclude('dummyThing')

    classifier = "working"
}

repositories {
    jcenter()
    maven { url 'https://repo.spongepowered.org/maven/' }
    maven { url 'https://jitpack.io' }
    flatDir {
        dirs 'deps'
    }
}

mixin {
    add sourceSets.main, "mixins.sbb.refmap.json"
}

sourceSets {
    main {
        output.resourcesDir = output.classesDir
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest.attributes(
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'MixinConfigs': 'mixins.sbb.json',
            'FMLCorePlugin': 'ga.matthewtgm.skyblockmod.mixins.SkyBlockModLoadingPlugin',
            'FMLCorePluginContainsFMLMod': true,
            'ForceLoadAsMod': true
    )
}


reobf {
    shadowJar {
        mappingType = 'SEARGE'
    }
}


task runClientFix {
    doLast {
        String fileName = "${archivesBaseName}-${version}-working.jar"
        ant.move file: "${buildDir}/libs/${fileName}", tofile: "${projectDir}/run/mods/${fileName}"
        ant.delete file: "${buildDir}/libs/${archivesBaseName}-${version}.jar"
    }
}

processResources
        {
            inputs.property "version", project.version
            inputs.property "mcversion", project.minecraft.version

            from(sourceSets.main.resources.srcDirs) {
                include 'mcmod.info'

                expand 'version': project.version, 'mcversion': project.minecraft.version
            }

            from(sourceSets.main.resources.srcDirs) {
                exclude 'mcmod.info'
            }
        }

tasks.reobfJar.dependsOn(tasks.shadowJar)
tasks.runClient.dependsOn(tasks.build)
tasks.runClient.dependsOn(runClientFix)