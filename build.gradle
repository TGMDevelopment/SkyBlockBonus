buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            url = "https://repo.spongepowered.org/maven/"
        }
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
        classpath 'org.spongepowered:mixingradle:0.6-SNAPSHOT'
    }
}
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'

version = project.mod_version
group = project.mod_group
archivesBaseName = project.mod_name

sourceCompatibility = targetCompatibility = 1.8

CustomDep[] deps = [
    new CustomDep("com.github.TGMDevelopment:SkyBlock-Bonus-API:0.1.2", true),
    new CustomDep("com.jagrosh:DiscordIPC:0.4", true),
    new CustomDep("com.github.TGMDevelopment:JsonTGM:0.2.8", true),
    new CustomDep("com.github.TGMDevelopment:TGMLib:0.7.7.5", true)
]

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = "run"

    replace("@VER@": project.version)

    mappings = "stable_22"
    clientJvmArgs += "-Dfml.coreMods.load=ga.matthewtgm.skyblockmod.mixins.SkyBlockModTweaker"
}

dependencies {
    compile('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        exclude module: 'launchwrapper'
        exclude module: 'guava'
        exclude module: 'gson'
        exclude module: 'commons-io'
    }
	annotationProcessor('org.spongepowered:mixin:0.7.11-SNAPSHOT')

    compileOnly('org.projectlombok:lombok:1.18.16')
    annotationProcessor('org.projectlombok:lombok:1.18.16')

    for (CustomDep dep : deps)
        compile(dep.name)
}

shadowJar {
    dependencies {
        include(dependency('org.spongepowered:mixin:0.7.11-SNAPSHOT'))
        for (CustomDep dep : deps)
            if (dep.embed)
                include(dependency(dep.name))
    }

    exclude('LICENSE.txt')
    exclude('dummyThing')

    classifier = project.mod_mixins_classifier
}

repositories {
    jcenter()
    maven { url 'https://repo.spongepowered.org/maven/' }
    maven { url 'https://jitpack.io' }
    flatDir {
        dirs 'deps'
    }
}

mixin {
    add sourceSets.main, "mixins.sbb.refmap.json"
}

sourceSets {
    main {
        output.resourcesDir = output.classesDir
    }
}

jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest.attributes(
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'MixinConfigs': 'mixins.sbb.json',
            'FMLCorePlugin': 'ga.matthewtgm.skyblockmod.mixins.SkyBlockModTweaker',
            'FMLCorePluginContainsFMLMod': true,
            'ForceLoadAsMod': true
    )
}


reobf {
    shadowJar {
        mappingType = 'SEARGE'
    }
}


task runClientFix {
    doLast {
        String fileName = "${archivesBaseName}-${version}-working.jar"
        ant.move file: "${buildDir}/libs/${fileName}", tofile: "${projectDir}/run/mods/${fileName}"
        ant.delete file: "${buildDir}/libs/${archivesBaseName}-${version}.jar"
    }
}

processResources
        {
            inputs.property "version", project.version
            inputs.property "mcversion", project.minecraft.version

            from(sourceSets.main.resources.srcDirs) {
                include 'mcmod.info'

                expand 'version': project.version, 'mcversion': project.minecraft.version
            }

            from(sourceSets.main.resources.srcDirs) {
                exclude 'mcmod.info'
            }
        }

tasks.reobfJar.dependsOn(tasks.shadowJar)
tasks.runClient.dependsOn(tasks.build)
tasks.runClient.dependsOn(runClientFix)

class CustomDep {
    final String name
    final boolean embed
    CustomDep(String name, boolean embed) {
        this.name = name
        this.embed = embed
    }
}